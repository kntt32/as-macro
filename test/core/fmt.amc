import core;
import cmp;
import slice;
import str;
import option;

pub template Display(Formatter) {
    pub as display(inout self: $(Formatter)@mem, in object: Str@mem) {
        self.write(object);
    };

    pub as display(inout self: $(Formatter)@mem, in object: i64@reg+mem+imm) {
        let object_r8: i64@r8 = object;
        let object_copy: i64@rax = object_r8;
        let str: Str@stack;
        let flag: bool@r9;

        if(flag.lt(object_copy, 0)) {
            str.from("-");
            self.write(str);
            object_copy.neg();
        };

        if(flag.eq(object_copy, 0)) {
            str.from("0");
            self.write(str);
        }else {
            let buffer: [char; 32]@stack;
            buffer[31] = '\0';
            let buffer_index: i64@r10 = 31;
            let buffer_ptr: *char@r11;
        
            let remainder: i64@rdx;
            let divisor: i64@r12 = 10;
        
            while(flag.neq(object_copy, 0)) {
                remainder = 0;
                object_copy.div(remainder, divisor);
                let remainder_i8: i8@r13 = remainder;

                buffer_index -= 1;
                buffer_ptr =& buffer;
                buffer_ptr += buffer_index;
                *buffer_ptr = '0';
                *buffer_ptr += remainder_i8;
            };

            buffer_ptr =& buffer;
            buffer_ptr += buffer_index;
            
            str.from(buffer_ptr);
            self.write(str);
        }
    };
};

