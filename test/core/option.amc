import core;

pub template Option(T) {
    pub struct Option_$(T) {
        flag: bool,
        body: union {
            object: $(T)
        },
    };

    pub as mov(out dst: Option_$(T)@mem, in src: Option_$(T)@mem) {
        dst.flag = src.flag;

        if(src.flag) {
            dst.body.object = src.body.object;
        };
    };

    pub as clone(in self: Option_$(T)@mem, out buff: Option_$(T)@mem) {
        buff.flag = self.flag;
        if(self.flag) {
            self.body.object.clone(buff.body.object);
        };
    };
    
    pub as some(out self: Option_$(T)@mem, in object: $(T)@reg+mem+imm) {
        self.flag = true;
        self.body.object = object;
    };

    pub as none(out self: Option_$(T)@mem) {
        self.flag = false;
    };

    pub as is_some(in self: Option_$(T)@reg, out flag: bool@reg+mem) {
        flag = self.flag;
    };

    pub as eq(out flag: bool@reg+mem, in left: Option_$(T)@mem, out right: Option_$(T)@mem) {
        flag = left.flag;
        if(flag.and(right.flag)) {
            flag.eq(left.body.object, right.body.object);
        }else {
            flag = left.flag;
            flag.or(right.flag).not();
        };
    };

    pub as take(inout self: Option_$(T)@mem, out object: $(T)@reg+mem) {
        if(self.flag) {
            object = self.body.object;
        }else {
            panic("can't take object from none");
        };
        self.flag = false;
    };

    pub as drop(inout self: Option_$(T)@mem) {
        if(self.flag) {
            self.body.drop();
        };
        self.flag = false;
    };
};

pub template Display_Option(Formatter, T) {
    pub as display(inout self: $(Formatter)@mem, in object: Option_$(T)@mem) {
        let str: Str@stack;

        if(object.flag) {
            str.from("Some(");
            self.write(str);
            self.display(object.body.object);
            str.from(")");
            self.write(str);
        }else {
            str.from("None");
            self.write(str);
        };
    };
};

