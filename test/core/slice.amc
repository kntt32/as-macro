import core;
import panic;

pub template Slice(T) {
    pub struct Slice_$(T) {
        ptr: *$(T),
        len: i64,
    };

    pub as mov(out dst: Slice_$(T)@mem, out src: Slice_$(T)@mem) {
        dst.ptr = src.ptr;
        dst.len = src.len;
    };

    pub as from(out self: Slice_$(T)@mem, in ptr: *$(T)@reg+mem, in len: i64@reg+mem+imm) {
        self.ptr = ptr;
        self.len = len;
    };

    pub as empty(out self: Slice_$(T)@mem) {
        self.ptr = null;
        self.len = 0;
    };
    
    pub as index(in self: Slice_$(T)@mem, in index: i64@reg+mem+imm, out ptr: *$(T)@reg+mem) {
        panic_ge(index, self.len, "Slice:index: out of range");
        ptr = sizeof($(T));
        ptr.imul(index);
        ptr += self.ptr;
    };

    pub as len(in self: Slice_$(T)@mem, out len: i64@reg+mem) {
        len = self.len;
    };

    pub as as_ptr(in self: Slice_$(T)@mem, out ptr: *$(T)@reg+mem) {
        ptr = self.ptr;
    };

    pub as slice(in self: Slice_$(T)@mem, in start: i64@reg+mem+imm, in len: i64@reg+mem+imm, out buff: Slice_$(T)@mem) {
        panic_le(self.len, start, "Slice:slice: out of range");
        panic_lt(len, 0, "Slice:slice: out of range");

        buff.ptr = sizeof($(T));
        buff.ptr.imul(start);
        buff.ptr += self.ptr;

        buff.len = len;
    };
};

